plugins {
    id 'java'
    id 'net.kyori.blossom' version "1.1.0" apply false
    id "com.github.johnrengelman.shadow" version "4.0.4" apply false
}

version = modVersion
group = modGroup
archivesBaseName = modBaseName

subprojects {
    buildscript {
        repositories {
            maven { url = 'https://files.minecraftforge.net/maven' }
            jcenter()
            mavenCentral()
        }
    }
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "net.kyori.blossom"
    apply plugin: "com.github.johnrengelman.shadow"

    version = rootProject.getVersion()
    group = rootProject.getGroup()
    archivesBaseName = rootProject.getArchivesBaseName()

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io/' }
        maven {
            name = "Forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }

    configurations {
        ship
        compile.extendsFrom ship
    }

    shadowJar {
        classifier = ''
        configurations = [project.configurations.ship]
        from sourceSets.main.output
    }

    artifacts {
        archives shadowJar
    }

    jar {
        manifest {
            attributes([
                    "Specification-Title": rootProject.getName(),
                    "Specification-Vendor": "ijo42",
                    "Specification-Version": "1", // We are version 1 of ourselves
                    "Implementation-Title": rootProject.getName(),
                    "Implementation-Version": rootProject.getVersion(),
                    "Implementation-Vendor" :"ijo42",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        options.incremental = true // one flag, and things will get so MUCH faster
    }
    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
}
tasks.getByPath(':jar').enabled = false
defaultTasks 'build'
